namespace FSIRunner

open System.IO
open System.Xml

module GenProject =
     let generate inputFSProjectFile outFile = 
        let text = File.ReadAllText(inputFSProjectFile)

        // This XML code is ripped/modified from one of the FSProjects, but I can't remember which one
        let document = 
            let xd = new XmlDocument()
            xd.LoadXml text
            xd

        let nsmgr = 
            let nsmgr = new XmlNamespaceManager(document.NameTable)
            nsmgr.AddNamespace("default", document.DocumentElement.NamespaceURI)
            nsmgr

        let compileNodesXPath = "/default:Project/default:ItemGroup/default:Compile"
        let getCompileNodes (document:XmlDocument) =         
            [for node in document.SelectNodes(compileNodesXPath,nsmgr) -> node]
        let getCompiledFiles() =
            let cnodes = getCompileNodes document
            let cfiles = 
                cnodes 
                |> Seq.map (fun n -> 
                    n.Attributes.GetNamedItem("Include").Value)
                |> Seq.filter (fun n -> n.ToLowerInvariant() <> "assemblyinfo.fs" ) // don't need this
            cfiles

        let refsXPath = "/default:Project/default:ItemGroup/default:Reference"
        let getReferenceNodes (document:XmlDocument) =         
            [for node in document.SelectNodes(refsXPath,nsmgr) -> node]
        let getReferenceFiles() =
            let rnodes = getReferenceNodes document
            let implicit = [ "mscorlib" ; "FSharp.Core" ; "System" ; "System.Core" ; "System.Numerics" ] |> List.map (fun s -> s.ToLowerInvariant())

            let getChildNodeValue (childName:string) (n:XmlNode) :string option =
                let childName = childName.ToLowerInvariant()
                let cnode = (Seq.cast n.ChildNodes) |> Seq.tryFind (fun (cn:XmlNode) -> cn.Name.ToLowerInvariant() = childName )
                match cnode with 
                | Some cn when (cn.InnerText.Trim() <> "") -> Some (cn.InnerText)
                | None -> None
                | Some cn -> None

            let rfiles = 
                rnodes 
                |> Seq.choose (fun n ->
                    let includeVal = n.Attributes.GetNamedItem("Include").Value.ToString()
                    let includeValLwr = includeVal.ToLowerInvariant()

                    match (List.tryFind (fun ival -> ival = includeValLwr) implicit) with
                    | Some x ->
                        None // don't include implicit references
                    | None ->
                        // include this reference; use HintPath if it exists, otherwise append ".dll" to the reference name
                        let hintPath = getChildNodeValue "hintpath" n

                        let includeDll = includeVal + ".dll" 
                        match hintPath with 
                        | None -> Some (includeDll)
                        | Some hp -> Some (hp))
                |> Seq.map (fun rpath -> rpath.Replace("\\", "/"))
            rfiles

        let sw = new StringWriter()
        do 
            sw.WriteLine("// this file is automatically generated by GenProjectPlugin.fsx")
            getReferenceFiles() |> Seq.iter (fun r -> sw.WriteLine("#r \"" + r + "\""))
            getCompiledFiles() |> Seq.iter (fun f -> sw.WriteLine("#load \"" + f + "\""))
            //printfn "%s" (sw.ToString())

            let outFile = Path.Combine(Directory.GetParent(inputFSProjectFile).FullName, outFile)
            printfn "Writing updated project to: %s" outFile
            File.WriteAllText(outFile, (sw.ToString()))
